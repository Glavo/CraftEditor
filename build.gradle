plugins {
    id 'java'
}

group 'org.glavo'
version '1.0-beta1-SNAPSHOT'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    // https://mvnrepository.com/artifact/org.controlsfx/controlsfx
    compile group: 'org.controlsfx', name: 'controlsfx', version: '8.40.14'

    testCompile group: 'junit', name: 'junit', version: '4.12'
}

task release(type: Jar) {
    group = 'build'
    baseName = project.name
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    manifest {
        attributes 'Implementation-Version': '1.2',
                'Main-Class': 'org.glavo.nbt.Main',
                'Multi-Release': 'true',
                'Add-Exports': 'javafx.graphics/com.sun.javafx.tk',
                'CraftEditor-Version': project.version
    }
    with jar
}

jar.baseName = project.name + "-core"

task run(type: JavaExec, dependsOn: release) {
    main = "-jar"
    //noinspection GroovyAssignabilityCheck
    args release.archivePath
}

processResources {
    filteringCharset 'UTF-8'
}

tasks.withType(ProcessResources).each { task ->
    task.from(task.getSource()) {
        include '**/*.properties'
        filter(org.apache.tools.ant.filters.EscapeUnicode)
    }
}